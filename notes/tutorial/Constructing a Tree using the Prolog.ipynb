{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Constructing a Tree using the Prolog\n",
    "\n",
    "* Review some topics from the course.\n",
    "* Learn how to reperesent the trees in Prolog.\n",
    "* Implement some tree related operations in Prolog.\n",
    "\n",
    "\n",
    "### Tuples\n",
    "\n",
    "One of the simplest ways of building compound data structures is by using the tupes. Here is instances of tuples in Haskell-like programing language:\n",
    "\n",
    "``` text\n",
    "(1,2,3):(Int, Int, Int)\n",
    "(\"Hello\",1): (String, Int)\n",
    "(\"Hello\"):(String)\n",
    "():()\n",
    "```\n",
    "\n",
    "What is the difference between lists and tuples?\n",
    "\n",
    "* Tuples are always heterogeneous, whereas lists are often homogeneous.\n",
    " * (that is, tuples can contain a mixture of types).\n",
    "* Tuples have a fixex length (built into the type).\n",
    "\n",
    "So, how we can present tuples in prolog?\n",
    "\n",
    "* A Prolog compound term of the form **label(a1,…,an)** can be viewed as an *n*-ary tuple along with the label *label*, and **we will use this fact to construct trees in Prolog**.\n",
    "\n",
    "\n",
    "## The original tree types\n",
    "\n",
    "\n",
    "Lets recall the types **BinTree** and **LeafTree** from homework 1:\n",
    "\n",
    "``` scala\n",
    "sealed trait LeafTree[A]\n",
    "case class Leaf[A](a: A) extends LeafTree[A]\n",
    "case class Branch[A](l: LeafTree[A], r: LeafTree[A]) extends LeafTree[A]\n",
    "\n",
    "sealed trait BinTree[A]\n",
    "case class Empty[A]() extends BinTree[A]\n",
    "case class Node[A](l: BinTree[A], a: A, r: BinTree[A]) extends BinTree[A]\n",
    "```\n",
    "\n",
    "For the reminder of the course, if we discuss these types, we will assume **constructors** of these shape.)\n",
    "\n",
    "## Tupling the arguments\n",
    "\n",
    "Consider the parameters of each constructor.\n",
    "- **_Leaf_** has a single parameter of type ***A***.\n",
    "- ***Branch*** has two parameters of type ***LeafTree A***.\n",
    "- ***Empty*** does have a parameter, of type ***Unit***.\n",
    "  - The only value of type ***Unit*** being ***()***.\n",
    "- ***Node*** has three parameters of types ***BinTree A***, ***A***, and ***BinTree A***.\n",
    "\n",
    "Considering this we could isomorphically define constructors which each took a single **tuple** as parameter.\n",
    "\n",
    "- ***Leaf*** would have a parameter of type ***Tuple1[A]***.\n",
    "  - To construct a singleton tuple with value ***v***, use ***Tuple1(v)***.\n",
    "  - For instance, ***Tuple1(5) : Tuple1[Int]***.\n",
    "- ***Branch*** would have a parameter of type ***Tuple2[LeafTree A, LeafTree A]***.\n",
    "- ***Empty*** is the same as ***Empty***, taking a parameter of type ***Unit***.\n",
    "  - There is no ***Tuple0*** type in Scala, but ***Unit*** is ***isomorphic***.\n",
    "- ***Node*** would have a parameter of type ***Tuple3[BinTree A, A, BinTree A]***.\n",
    "\n",
    "We have to say ***isomorphically*** rather than ***equivalently*** because\n",
    "these constructors as **not** equivalent to the previous versions\n",
    "(except for **Empty**.) But they are **isomorphic**, because they can represent\n",
    "the same trees, and we have a 1-1 correspondence between them.\n",
    "\n",
    "The Haskell naming of the tuple type would make\n",
    "these descriptions briefer.\n",
    "- ***Leaf*** would have a parameter of type ***(A)***.\n",
    "- ***Branch*** would have a parameter of type ***(LeafTree A, LeafTree A)***.\n",
    "- ***Empty*** would have a parameter of type ***()***.\n",
    "- ***Node*** would have a parameter of  type ***(BinTree A, A, BinTree A)***.\n",
    "\n",
    "## Trees without constructors\n",
    "\n",
    "Given the above constructors using tuples, we can see that we could even ***omit*** the constructors\n",
    "and simply write trees ***as tuples***. For instance,\n",
    "\n",
    "\n",
    "``` scala\n",
    "Branch(Leaf(1),Branch(Leaf(2),Leaf(3))) : LeafTree[Int]\n",
    "```\n",
    "corresponds to the tuple\n",
    "\n",
    "``` scala\n",
    "(1,(2,3)) : Tuple2[Int,Tuple2[Int,Int]]\n",
    "```\n",
    "\n",
    "***They are not the same type, but they represent the same tree.***\n",
    "\n",
    "Of course we may introduce confiusion and junk in the same time. However, introducting of confiusion is not a really big problem, since by having look at the ***shape*** of the tuple we would understand the kind of tree it represent. But we will introduce some junks. ***Because there is no static way to seperate a tuple that represent a tree from the tuples that represent other kind of data***\n",
    "\n",
    "\n",
    "- In a statically typed language such as Scala and Haskell,this method of representation is practically unusable for this reason.\n",
    "\n",
    "- But in a *dynamically* typed language (we encourage you to read “dynamically typed” as “dynamically type checked”, as Pierce suggests in his chapter 1) where no types are checked until runtime, this approach is feasible, and in the absence of user-defined types, necessary!\n",
    "\n",
    "## Recognising trees \n",
    "\n",
    "Recall that a Prolog compound term of the form ***label(a1,…,an)*** can be viewed\n",
    "as an **n**-ary tuple along with the label ***label**. We will use the labels to indicate the constructor we have in mind when constructing trees as tuples.\n",
    "\n",
    "So, for the ***LeafTree*** type, we have trees such as\n",
    "\n",
    "``` prolog\n",
    "leaf(5)\n",
    "leaf([])\n",
    "branch(leaf(1),branch(leaf(2),leaf(3)))\n",
    "branch(branch(leaf(1),leaf(2)),leaf(3))\n",
    "```\n",
    "\n",
    "and for **BinTree**, examples include\n",
    "\n",
    "``` prolog\n",
    "empty\n",
    "node(empty,1,empty)\n",
    "node(node(empty,'left element',empty),top_element,node(empty,3,empty))\n",
    "```\n",
    "\n",
    "Considering these definitions, we can construct predicates to check our two tree “types”.\n",
    "\n",
    "These allow for *runtime* checking that arguments have the “correct type” (Type checkers).\n",
    "\n",
    "``` prolog\n",
    "isBinTree(empty).\n",
    "isBinTree(node(L,_,R)) :- isBinTree(L), isBinTree(R).\n",
    "\n",
    "isLeafTree(leaf(_)).\n",
    "isLeafTree(branch(L,R)) :- isLeafTree(L), isLeafTree(R).\n",
    "```\n",
    "\n",
    "There is nothing said with the above predicates about the data inside the trees. So we can easily have trees mix togeather different kinds of data. But the big question to ask is do we really need to check if the tree has the right shape? and the data in the tree has the same type? As an example we can see the list in prolog."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "X = [a, 1].\n",
      "\n",
      "\n"
     ]
    }
   ],
   "source": [
    "%%script swipl -q\n",
    "append([a],[1],X)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Operations on Trees\n",
    "\n",
    "Let's implement some basic operations on our tree type. The **flatten** and **orderedElems** operations\n",
    "from homework 1 will be assigned as **homework**.\n",
    "\n",
    "``` prolog\n",
    "% Inserting into the empty tree creates a node containing E,\n",
    "% with empty subtrees.\n",
    "binInsert(E,\n",
    "          empty,\n",
    "          node(empty,E,empty)).\n",
    "          \n",
    "\n",
    "```\n",
    "\n",
    "To insert into a non-empty `BinTree` (a `node`) we must use a recursive clause.\n",
    "\n",
    "Naively, we might want to write, for instance, `binInsert(E, node(L,A,R), node(binInsert(E,L),A,R))`. \n",
    "\n",
    "- Notice how what we intend to be the ***“recursive call”*** is **not** the same predicate; ~binInsert~ has **three** arguments, not **two**.\n",
    "\n",
    "- In any case, a predicate is either true or false; it doesn't return a “value”.\n",
    "\n",
    "\n",
    "So we need a recursive premise instead.\n",
    "\n",
    "``` prolog\n",
    "\n",
    "% Inserting into a node\n",
    "% inserts it into the left subtree.\n",
    "% (This implementation arbitrarily chosen.)\n",
    "% \n",
    "binInsert(E,\n",
    "          node(L,A,R),\n",
    "          node(NL,A,R)) :- binInsert(E,L,NL). % NL for \"New Left\" \n",
    "          \n",
    "```\n",
    "\n",
    "In the above, we made an arbitrary choice about where to insert the new element. Specifically, we inserted it as far left as we could. This is a decent choice, as far as it goes; but note that in a logical language, we don't really have to make a choice! ***We can give as many recursive clauses as we like, and then when a user makes an insert query, they could choose the response (solution) that best fits their need.***\n",
    "\n",
    "``` prolog\n",
    "\n",
    "% Inserting into BinTrees *nondeterministically*.\n",
    "% This version could be made to produce all possible valid inserts!\n",
    "\n",
    "% There's only one way to insert into the empty tree.\n",
    "binInsertND(E,empty,node(empty,E,empty)).\n",
    "\n",
    "% But there are at least 2 ways we can insert into a nonempty tree.\n",
    "binInsertND(E,node(L,A,R),node(NL,A,R)) :- binInsertND(E,L,NL).\n",
    "binInsertND(E,node(L,A,R),node(L,A,NR)) :- binInsertND(E,R,NR).\n",
    " \n",
    "```\n",
    "\n",
    "Lets try this quickly to see if it works?\n",
    "\n",
    "``` text\n",
    "\n",
    "binInsert('hello world!!',empty,X).\n",
    "\n",
    "binInsert('hello worl!!', X, node(empty, 'hello worl!!', empty)).\n",
    "\n",
    "```\n",
    "\n",
    "Now based on what we did for `BinTree`, lets see how we can write the same clauses for `leafTree`:\n",
    "\n",
    "``` prolog\n",
    "\n",
    "% Inserting into a leaf results in a branch with two leaves.\n",
    "leafInsert(E,\n",
    "           leaf(A),\n",
    "           branch(leaf(A),leaf(E))).\n",
    "\n",
    "% Inserting into a branch inserts it into the right subtree.\n",
    "% (again, this is an arbitrary choice.)\n",
    "leafInsert(E,\n",
    "           branch(L,R),\n",
    "           branch(L,NR)) :- leafInsert(E,R,NR).\n",
    "\n",
    "\n",
    "\n",
    "% Inserting into LeafTrees nondeterministically.\n",
    "\n",
    "% We have two choices for inserting into a leaf.\n",
    "leafInsertND(E,\n",
    "             leaf(A),\n",
    "             branch(leaf(A),leaf(E))).\n",
    "leafInsertND(E,\n",
    "             leaf(A),\n",
    "             branch(leaf(E),leaf(A))).\n",
    "\n",
    "% And there are at least 2 ways we can insert into a branch.\n",
    "leafInsertND(E,\n",
    "             branch(L,R),\n",
    "             branch(L,NR)) :- leafInsertND(E,R,NR).\n",
    "leafInsertND(E,\n",
    "             branch(L,R),\n",
    "             branch(NL,R)) :- leafInsertND(E,L,NL).\n",
    "\n",
    "```\n",
    "\n",
    "Lets test this implementations too see if it works at its desired?\n",
    "\n",
    "\n",
    "``` text\n",
    "\n",
    "leafInsert(6, branch(branch(leaf(1),leaf(2)), branch(leaf(3),leaf(4))),X).\n",
    "\n",
    "```\n",
    "\n",
    "What if we try the nondeterministic implementation?\n",
    "\n",
    "\n",
    "``` text\n",
    "\n",
    "leafInsertND(6, branch(branch(leaf(1),leaf(2)), branch(leaf(3),leaf(4))),X).\n",
    "\n",
    "```\n",
    "\n",
    "Now lets implement the `binDelete` based on the above implementation.\n",
    "\n",
    "``` prolog\n",
    "\n",
    "binDelete(E, node(empty,E,empty),empty).\n",
    "binDelete(E, node(L,A,R), node(NL,A,R)) :- binDelete(E, L, NL).\n",
    "\n",
    "```\n",
    "\n",
    "Lets test it out:\n",
    "\n",
    "``` text\n",
    "\n",
    "binDelete('h', X, node(empty, 'hi', 'habib')).\n",
    "binDelete('h', X, node(empty, 'hi', node(empty,'habib',empty))).\n",
    "binDelete('h', node(node(empty,'h',empty),'hi', node(empty,'habib',empty)), X).\n",
    "\n",
    "```\n",
    "\n",
    "***Exercise: Implement the nonedeterminstic version of delete for both Bin and Leaf trees.***\n",
    "\n",
    "As practice in tutorial, we worked out a way to “join” two trees together. \n",
    "\n",
    "\n",
    "``` prolog\n",
    "leafJoin(leaf(E1),\n",
    "         leaf(E2),\n",
    "         branch(leaf(E1),leaf(E2))).\n",
    "leafJoin(leaf(E1),\n",
    "         branch(L,R),\n",
    "         branch(L,branch(leaf(E1),R))).\n",
    "leafJoin(branch(L,R),\n",
    "         leaf(E2),\n",
    "         branch(L,branch(leaf(E2),R))).\n",
    "leafJoin(branch(L1,R1),\n",
    "         branch(L2,R2),\n",
    "         branch(branch(L1,R1),branch(L2,R2))).\n",
    "```\n",
    "\n",
    "Now lets see if this is workig?\n",
    "\n",
    "``` text\n",
    "\n",
    "leafJoin(branch(leaf(1),leaf(2)), branch(leaf(3),leaf('hi')),X).\n",
    "\n",
    "```\n",
    "\n",
    "Not necessarly correct but arbitary implementation:\n",
    "\n",
    "``` prolog\n",
    "\n",
    "leafJoin(leaf(E1),\n",
    "         branch(L,R),\n",
    "         branch(NL,R)) :- leafJoin(leaf(E1),L,NL)\n",
    "\n",
    "```\n",
    "\n",
    "\n",
    "\n",
    "What exactly this implementation doing?\n",
    "\n",
    "***Exercise: Implement the nonedeterminstic version of joint for both Bin and Leaf trees.***\n",
    "\n",
    "\n",
    "\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
